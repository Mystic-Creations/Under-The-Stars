plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version //for file building
    mod_version = rootProject.mod_version
    minecraft_version = rootProject.minecraft_version
    fabric_loader_version = rootProject.fabric_loader_version
    architectury_api_version = rootProject.architectury_api_version
    terrablender_version = rootProject.terrablender_version

    repositories {
    }
}

def isDevBuild = (findProperty("dev_builds")?.toString()?.toBoolean() ?: false)

def gitCommitCount = providers.exec {
    commandLine "git", "rev-list", "--count", "HEAD"
}.standardOutput.asText.map { it.trim() }

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base { archivesName = rootProject.archives_name }

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    def detectedPlatform
    def name = project.name.toLowerCase()
    if (name.contains('fabric')) {
        detectedPlatform = 'Fabric'
    } else if (name.contains('forge')) {
        detectedPlatform = 'Forge'
    } else {
        detectedPlatform = 'Unknown'
    }

    if (detectedPlatform) {
        def baseVersion = "${rootProject.mod_version}+mc${rootProject.minecraft_version}-${detectedPlatform}"

        if (isDevBuild) {
            project.version = baseVersion + "-build.${gitCommitCount.get()}"
        } else {
            project.version = baseVersion
        }
    } else {
        project.version = rootProject.mod_version
    }

    tasks.withType(Jar).configureEach { Jar jarTask ->
        jarTask.archiveBaseName.set(rootProject.archives_name)
        jarTask.archiveVersion.set(project.version.toString())
    }

    tasks.matching { it.name == 'shadowJar' }.configureEach { task ->
        if (task.hasProperty('archiveBaseName')) {
            task.archiveBaseName.set(rootProject.archives_name)
            task.archiveVersion.set(project.version.toString())
        }
    }
}